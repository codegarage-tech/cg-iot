apply plugin: 'com.android.application'

android {
    compileSdkVersion appCompileSdkVersion
    buildToolsVersion appBuildToolsVersion

    signingConfigs {
        releaseConfig {
            try {
                keyAlias 'keydata.keystore'
                keyPassword KEY_PASSWORD
                storeFile file('../misc/keydata.keystore')
                storePassword STORE_PASSWORD
            } catch (ex) {
                throw new InvalidUserDataException("You should define KEY_PASSWORD and STORE_PASSWORD in gradle.properties.")
            }
        }
    }

    defaultConfig {
        applicationId "tech.codegarage.iot"
        minSdkVersion appMinSdkVersion
        targetSdkVersion appTargetSdkVersion
//        versionCode appUserVersionCode
//        versionName appUserVersionName

        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
//        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // define your base Applications name here
        resValue 'string', 'app_name', 'IoT'
    }
    buildTypes {
        debug {
            shrinkResources false
            minifyEnabled false
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            shrinkResources true
            minifyEnabled true
            debuggable false

            //For single file
//            proguardFile 'aboutbox.pro'
            //For all files
//            proguardFiles fileTree(dir: "rules", include: ["*.pro"]).asList().toArray()

            //Default proguard call
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }

    //Flavor definition
    flavorDimensions "appType", "marketType"
    productFlavors {
        user {
            dimension "appType"

            applicationIdSuffix ".user"
            versionCode appUserVersionCode
            versionName appUserVersionName
        }

        bd {
            dimension "marketType"

//            applicationIdSuffix ".bd"
        }
        us {
            dimension "marketType"

//            applicationIdSuffix ".us"
        }
        uk {
            dimension "marketType"

//            applicationIdSuffix ".uk"
        }
    }

    //Remove unwanted vairant
    android.variantFilter { variant ->
        if (variant.getFlavors().get(0).name.equals('user')
                && (variant.getFlavors().get(1).name.equals('us')
                || variant.getFlavors().get(1).name.equals('uk'))) {
            variant.setIgnore(true)
        }
    }

    //Format output
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            println variant

            //Rename the output apk file
            def appName = variant.mergedFlavor.resValues.get('app_name').getValue()
            def underScore = "_"
            def hyphen = "-"
            def flavor = variant.baseName.split("-")[0]
            def version = variant.versionName
            def buildType = variant.buildType.name
            def releaseApkName = appName + underScore + convertToCamelCase(flavor) + underScore + version + underScore + convertToCamelCase(buildType) + ".apk"
            outputFileName = releaseApkName

            // set new resVale
            def finalAppName = ""
            if (flavor.equals('user')) {
                finalAppName = appName
            }
            variant.resValue 'string', 'app_name', finalAppName
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

def static String convertToCamelCase(String str) {
    if (str == null) {
        return null
    }

    boolean space = true
    StringBuilder builder = new StringBuilder(str)
    final int len = builder.length()

    for (int i = 0; i < len; ++i) {
        char c = builder.charAt(i)
        if (space) {
            if (!Character.isWhitespace(c)) {
                // Convert to title case and switch out of whitespace mode.
                builder.setCharAt(i, Character.toTitleCase(c))
                space = false
            }
        } else if (Character.isWhitespace(c)) {
            space = true
        } else {
            builder.setCharAt(i, Character.toLowerCase(c))
        }
    }

    return builder.toString()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Android support libraries
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:multidex:$multiDexVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"

    // Image loading
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$glideVersion"

    // Parceler
    implementation "org.parceler:parceler-api:$parcelerVersion"
    annotationProcessor "org.parceler:parceler:$parcelerVersion"

    // Retrofit for server communication
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'

    // Barcode
    implementation "com.journeyapps:zxing-android-embedded:$zxingVersion"

    // Location library
    implementation "com.google.android.gms:play-services-location:$googlePlayServiceLocationVersion"

    // Views
    implementation 'io.github.luizgrp.sectionedrecyclerviewadapter:sectionedrecyclerviewadapter:1.2.0'

    // Test strategy
//    implementation "junit:junit:$junitVersion"
//    implementation "com.android.support.test:runner:$androidTestRunnerVersion"
//    implementation "com.android.support.test.espresso:espresso-core:$espressoCoreVersion"

    // Animation
    implementation 'com.labo.kaji:fragmentanimations:0.1.1'
    implementation 'com.github.ali-sardari:AnimUtils:1.0.1'
//    implementation 'com.facebook.rebound:rebound:0.3.8'
    //https://github.com/cywmm/AnimationUtil

    // Util
    implementation "com.reversecoder.library:rc-library:$libraryVersion"

    // Library projects
    implementation project(":library:ribblemenu")
    implementation project(":library:statusbarutil")
    implementation project(":library:niftydialogeffects")
    implementation project(":library:verticalstepperform")
    implementation project(":library:slideupandroid")
    implementation project(":library:esptouch")
    implementation project(":library:easyrecyclerview")
}